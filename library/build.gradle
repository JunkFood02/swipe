plugins {
  alias(libs.plugins.android.library)
  alias(libs.plugins.kotlin.android)
  alias(libs.plugins.mavenPublish)
  alias(libs.plugins.paparazzi)
}

android {
  resourcePrefix "swipe_"
  namespace "me.saket.swipe"

  defaultConfig {
    minSdk libs.versions.minSdk.get().toInteger()
    compileSdk libs.versions.compileSdk.get().toInteger()
  }
  buildFeatures {
    compose = true
  }
  composeOptions {
    kotlinCompilerExtensionVersion = libs.versions.androidx.compose.compiler.get()
  }
  java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(11))
  }
  lintOptions {
    abortOnError true
  }
}

dependencies {
  implementation libs.compose.ui
  implementation libs.compose.foundation

  testImplementation libs.junit
  testImplementation libs.compose.material3
  testImplementation libs.compose.materialIcons
  testImplementation libs.androidx.savedstate
  testImplementation libs.androidx.lifecycle
}

// Used on CI to prevent publishing of non-snapshot versions.
tasks.register('throwIfVersionIsNotSnapshot') {
  def libraryVersion = project.findProperty("VERSION_NAME") as String
  if (!libraryVersion.endsWith("SNAPSHOT")) {
    throw new GradleException("Project isn't using a snapshot version = $libraryVersion")
  }
}
